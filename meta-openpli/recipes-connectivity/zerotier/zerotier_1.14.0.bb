SUMMARY = "ZeroTier is a smart programmable Ethernet switch for planet Earth"
MAINTAINER = "https://github.com/zerotier/ZeroTierOne"
DESCRIPTION = "Allows all networked devices, VMs, containers, and applications to communicate \
            as if they all reside in the same physical data center or cloud region."
HOMEPAGE = "https://www.zerotier.com"
SECTION = "net"
LICENSE = "MPL-2.0"
LIC_FILES_CHKSUM = "file://LICENSE-MPL.txt;md5=9741c346eef56131163e13b9db1241b3"

DEPENDS = "miniupnpd"
RRECOMMENDS:${PN} = "kernel-module-tun"

inherit gitpkgv

SRCREV = "${AUTOREV}"
PV = "1.14.0+git${SRCPV}"
PKGV = "1.14.0+git${GITPKGV}"

GIT_SITE = "${@ 'git://gitlab.com/jack2015' if d.getVar('CODEWEBSITE') else 'git://gitee.com/jackgee2021'}"
SRC_URI = "${GIT_SITE}/ZeroTierOne.git;protocol=https;branch=main \
        file://zerotier \
        file://0001-accept-external-ldflags.patch \
        file://0003-dont-enable-AES-hwcaps-for-arm.patch \
"

S = "${WORKDIR}/git"

INITSCRIPT_NAME = "zerotier"

inherit autotools-brokensep update-rc.d systemd

EXTRA_OEMAKE = " ZT_SSO_SUPPORTED=0 "

do_compile:prepend:mipsel() {
    export LDLIBS+=-latomic
}

do_install:append() {
    install -d ${D}${sysconfdir}/init.d
    install -m 0755 ${WORKDIR}/zerotier ${D}${sysconfdir}/init.d/zerotier
}

INSANE_SKIP:${PN} = "already-stripped ldflags"
